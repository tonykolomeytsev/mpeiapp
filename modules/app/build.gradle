plugins {
    alias(mpeix.plugins.app)
    alias(mpeix.plugins.app.signing)
    alias(libs.plugins.google.services)
    alias(libs.plugins.firebase.crashlytics)
}

apply(from: "${rootProject.projectDir}/gradle/ci.gradle")

android {
    namespace = "kekmech.ru.mpeiapp"

    defaultConfig {
        applicationId = "kekmech.ru.mpeiapp"
        versionName = "2.0.1"
        versionCode = 49
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            matchingFallbacks += ["debug"]
            signingConfig = signingConfigs.getByName("release")
        }
        qa {
            debuggable = false
            minifyEnabled = true
            shrinkResources = true
            applicationIdSuffix = ".dev"
            matchingFallbacks += ["debug"]
            signingConfig = signingConfigs.getByName("debug")
        }
        debug {
            minifyEnabled = false
            applicationIdSuffix = ".dev"
        }
    }
    lint {
        checkReleaseBuilds = false
        checkDependencies = true
        ignoreTestSources = true
        checkAllWarnings = false
        lintConfig = rootProject.file("lint.xml")
    }
    buildFeatures {
        viewBinding = true
    }
    packaging {
        // This is to make it possible to use Ktor (mock_server) on android
        // https://github.com/gnarea/minimal-ktor-server-android/blob/master/app/build.gradle
        resources.excludes += ["META-INF/**", "**/attach_hotspot_windows.dll"]
    }
    // https://developer.android.com/guide/app-bundle/configure-base#disable_config_apks
    bundle {
        // Because app has a runtime language selector
        language.enableSplit = false
    }
}

dependencies {
    implementation(libs.androidx.appCompat)
    implementation(libs.androidx.activity)
    implementation(libs.androidx.constraintLayout)
    implementation(libs.androidx.coordinatorLayout)
    implementation(libs.androidx.coreKtx)
    implementation(libs.androidx.lifecycleCommonJava8)
    implementation(libs.androidx.recyclerView)
    implementation(libs.androidx.viewPager2)
    implementation(libs.androidx.datastore.prefs)
    implementation(libs.androidx.splashscreen)
    implementation(libs.firebase.analytics)
    implementation(libs.firebase.config)
    implementation(libs.firebase.crashlytics)
    implementation(libs.google.material)
    implementation(libs.koin.android)
    implementation(libs.koin.core)
    implementation(libs.squareup.okhttp)
    implementation(libs.squareup.okhttp.logger)
    implementation(libs.squareup.retrofit)
    implementation(libs.squareup.retrofit.kser)
    implementation(libs.vivid.elmslie.android)
    implementation(libs.vivid.elmslie.core)
    implementation(libs.timber)

    implementation(project(":feature_app_settings_api"))
    implementation(project(":feature_app_settings_impl"))
    implementation(project(":feature_app_settings_impl"))
    implementation(project(":feature_app_update_api"))
    implementation(project(":feature_app_update_impl"))
    implementation(project(":feature_bars_api"))
    implementation(project(":feature_bars_impl"))
    implementation(project(":feature_contributors_api"))
    implementation(project(":feature_contributors_impl"))
    implementation(project(":feature_dashboard_api"))
    implementation(project(":feature_dashboard_impl"))
    implementation(project(":feature_favorite_schedule_api"))
    implementation(project(":feature_favorite_schedule_impl"))
    implementation(project(":feature_main_screen_api"))
    implementation(project(":feature_main_screen_impl"))
    implementation(project(":feature_map_api"))
    implementation(project(":feature_map_impl"))
    implementation(project(":feature_notes_api"))
    implementation(project(":feature_notes_impl"))
    implementation(project(":feature_onboarding_api"))
    implementation(project(":feature_onboarding_impl"))
    implementation(project(":feature_schedule_api"))
    implementation(project(":feature_schedule_impl"))
    implementation(project(":feature_search_api"))
    implementation(project(":feature_search_impl"))

    implementation(project(":ext_android"))
    implementation(project(":ext_koin"))
    implementation(project(":ext_kotlin"))
    implementation(project(":ext_shared_preferences"))

    implementation(project(":lib_analytics_android"))
    implementation(project(":lib_app_database"))
    implementation(project(":lib_app_info"))
    implementation(project(":lib_app_lifecycle"))
    implementation(project(":lib_coroutines"))
    implementation(project(":lib_elm"))
    implementation(project(":lib_feature_toggles"))
    implementation(project(":lib_navigation"))
    implementation(project(":lib_network"))
    implementation(project(":lib_persistent_cache"))
    implementation(project(":lib_snowfall"))

    implementation(project(":coreui"))
    implementation(project(":res_icons"))
    implementation(project(":res_strings"))

    debugImplementation(project(":mock_server"))
    qaImplementation(project(":mock_server"))
    debugImplementation(project(":debug_menu"))
    qaImplementation(project(":debug_menu"))
    debugImplementation(project(":lib_chucker"))
    qaImplementation(project(":lib_chucker"))

    testImplementation(libs.kotest.runner)
}
