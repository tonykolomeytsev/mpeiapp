// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: "gradle/ci.gradle"

buildscript {

    ext {
        versions = [
                kotlin              : "1.5.30",
                recyclerview        : "1.2.1",
                constraintlayout    : "2.1.0",
                coordinatorlayout   : "1.1.0",
                viewPager2          : "1.0.0",
                appcompat           : "1.1.0",
                koin                : "3.1.2",
                material            : "1.4.0",
                cardview            : "1.0.0",
                swiperefreshlayout  : "1.1.0",
                fragmentktx         : "1.3.6",
                retrofit            : "2.9.0",
                okhttp              : "4.9.0"
        ]
        kotlin = "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
        kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
        timber = "com.jakewharton.timber:timber:5.0.1"
        desugaring = "com.android.tools:desugar_jdk_libs:1.1.5"
        picasso = "com.squareup.picasso:picasso:2.8"
        androidx = [
                constraintlayout    : "androidx.constraintlayout:constraintlayout:${versions.constraintlayout}",
                coordinatorlayout   : "androidx.coordinatorlayout:coordinatorlayout:${versions.coordinatorlayout}",
                appcompat           : "androidx.appcompat:appcompat:${versions.appcompat}",
                recyclerview        : "androidx.recyclerview:recyclerview:${versions.recyclerview}",
                cardview            : "androidx.cardview:cardview:${versions.cardview}",
                swiperefreshlayout  : "androidx.swiperefreshlayout:swiperefreshlayout:${versions.swiperefreshlayout}",
                viewPager2          : "androidx.viewpager2:viewpager2:${versions.viewPager2}",
                fragmentKtx         : "androidx.fragment:fragment-ktx:1.3.6",
                coreKtx             : "androidx.core:core-ktx:1.3.2",
                lifecycleCommonJava8: "androidx.lifecycle:lifecycle-common-java8:2.3.1",
                lifecycleExtensions : "androidx.lifecycle:lifecycle-extensions:2.2.0",
                test: [
                        runner      : "androidx.test:runner:1.2.0",
                        espresso    : "androidx.test.espresso:espresso-core:3.2.0"
                ],
        ]
        google = [
                material            : "com.google.android.material:material:${versions.material}",
                gson                : "com.google.code.gson:gson:2.8.8",
                maps                : "com.google.android.gms:play-services-maps:17.0.1"
        ]
        koin = [
                core                : "io.insert-koin:koin-core:${versions.koin}",
                android             : "io.insert-koin:koin-android:${versions.koin}"
        ]
        firebase = [
                analytics           : "com.google.firebase:firebase-analytics:19.0.1",
                config              : "com.google.firebase:firebase-config:21.0.1",
                crashlytics         : "com.google.firebase:firebase-crashlytics:18.2.1"
        ]
        rx = [
                java                : "io.reactivex.rxjava3:rxjava:3.1.1",
                android             : "io.reactivex.rxjava3:rxandroid:3.0.0"
        ]
        squareup = [
                retrofit            : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                retrofit_rxJava2    : "com.squareup.retrofit2:adapter-rxjava3:${versions.retrofit}",
                retrofit_gson       : "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
                okhttp              : "com.squareup.okhttp3:okhttp:${versions.okhttp}",
                okhttpLogger        : "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"
        ]
        facebook = [
                shimmer             : "com.facebook.shimmer:shimmer:0.5.0"
        ]
        kotest = [
                runner              : "io.kotest:kotest-runner-junit5:4.4.1",
                assertions          : "io.kotest:kotest-assertions-core:4.4.1",
                property            : "io.kotest:kotest-property:4.4.2"
        ]
        tinkoff = [
                pagerindicator      : "ru.tinkoff.scrollingpagerindicator:scrollingpagerindicator:1.2.1"
        ]
        vividMoney = [
                elmslieCore         : "com.github.vivid-money.elmslie:elmslie-core:1.1.4",
                elmslieAndroid      : "com.github.vivid-money.elmslie:elmslie-android:1.1.4"
        ]
        appVersionName = "1.11.0"
        appVersionCode = 43
        gradleDir = "$rootDir/gradle"
    }

    repositories {
        google()
        maven { url "https://jitpack.io" }
        mavenCentral()
        maven {
            url "https://maven.fabric.io/public"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:4.3.10"
        classpath "com.google.firebase:firebase-crashlytics-gradle:2.7.1"
    }
}


allprojects {
    repositories {
        google()
        maven { url "https://jitpack.io" }
        mavenCentral()
    }
    configurations {
        detekt
    }
    dependencies {
        detekt "io.gitlab.arturbosch.detekt:detekt-cli:1.15.0"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detekt(type: JavaExec) {
    mainClass.set("io.gitlab.arturbosch.detekt.cli.Main")
    classpath = configurations.detekt
    def input = "$projectDir/modules"
    def config = "$projectDir/detekt.yaml"
    def exclude = ".*/build/.*,.*/resources/.*,*/.idea/.*"
    def params = [ '-i', input, '-c', config, '-ex', exclude]
    args(params)
}
